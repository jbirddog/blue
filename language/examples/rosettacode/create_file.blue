global _start

: syscall ( num:eax -- result:eax ) syscall ;

: exit ( status:edi -- noret ) 60 syscall ;
: bye ( -- noret ) 0 exit ;
: exit.syserr ( err:eax -- noret ) neg exit ;

: unwrap ( result:eax -- value:eax ) dup 0 cmp ' exit.syserr xl ;
: ordie ( result -- ) unwrap drop ;

: open ( pathname:edi flags:esi mode:edx -- fd:eax ) 2 syscall unwrap ;
: close ( fd:edi -- ) 3 syscall ordie ;

: mkdir ( pathname:edi mode:esi -- ) 83 syscall ordie ;

00001 const forWriting
00100 const create
01000 const truncate

create forWriting or truncate or const flags

: create-file ( pathname -- ) flags 0640 open close ;
: make-directory ( pathname -- ) 0750 mkdir ;

: create-files ( -- ) s" output.txt" drop create-file ;
: make-directories ( -- ) s" docs" drop make-directory ;

: _start ( -- noret ) create-files make-directories bye ;
